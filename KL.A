'|KL.A diverse Funktionen
'|     aus DiskWorks
'|(C)  1992 by KiKiSoft
'+-----------------------
PARAM: COM PARAMETER SET
' TO COM1,9600,8,N,1,X,N
LD C,SETCOM
LD D,COM1
LD HL,COMPAR
CALL SERIELL

'SETDEV "COM1:"
LD C,SETDEV
XOR A
JP SERIELL

COMPAR:
IFD CHRISTO
DEFB &08,&00,&5C
ELSE
DEFB &08,&00,&4C
ENDC

SHOWBL: ZEIGT BLOCK AN
' AB &F400 IN HEX/ASCII
PUSH HL
PUSH DE
LD HL,&F400

SB_01:
CALL SHOWLI
XOR A
LD (CURX),A
LD A,(CURY)
INC A
LD (CURY),A
CP 4
JR NZ,SB_03
DEC A
LD (CURY),A
PUSH HL
PUSH DE
PUSH BC
CALL INKEY
CP MODE
JR Z,SB_E1
CALL UPSCRL
POP BC
POP DE
POP HL

SB_03:
LD A,H
CP &F4
JR Z,SB_01

SB_EX:
POP DE
POP HL
RET

SB_E1:
SCF
POP BC
POP DE
POP HL
JR SB_EX

SHOWLI: SHOW LINE
PUSH BC
LD B,6
PUSH HL
PUSH BC

SL_01:
LD A,(HL)
CALL AOUT
LD A," "
CALL PRTAKKU
INC HL
DJNZ SL_01

POP BC
POP HL

SL_02:
LD A,(HL)
CALL PRTAKKU
INC HL
DJNZ SL_02
POP BC
RET

VIEW: FILE IM HEX/ASC-MODE
'     ANZEIGEN
'NUR VORWŽRTSBLŽTTERN
'M™GLICH!
LD A,(COUNT)
OR A
RET Z
CALL CLS
CURSOR &0000
CALL SV_FMSK
LD A,(FPTR)
PUSH AF
DEC A
CALL GFNADR
LD DE,FMASK
LD BC,FNLEN
LDIR
LD HL,&F400
LD (&FC46),HL
CALL OPENFF
JR C,VI_EX

VI_01:
LD DE,FCB
LD C,RD_FL
CALL FILE
JR C,VI_EX

CALL SHOWBL
JR C,VI_EX

JR VI_01

VI_EX:
CALL CLOSE1
CALL LD_FMSK
CALL RDSRC
CALL CLS
POP AF
LD (FPTR),A
RET

PROTECT: SCHREIBSCHUTZ
LD A,(COUNT)
OR A
RET Z
CALL SV_FMSK
LD A,(FPTR)
PUSH AF
DEC A
CALL GFNADR
LD DE,FMASK
LD BC,FNLEN
LDIR
CALL SETFCB

LD A,(FCB+&14)
CP &20 UNGESCHšTZT?
JR Z,PT_01

'A=&21
LD A,&20
JR PT_02

PT_01:
LD A,&21

PT_02:
LD (FCB+&14),A
LD DE,FCB
LD C,&1E
CALL FILE
CALL C,FEHLER

PT_EX:
CALL LD_FMSK
CALL RDSRC
POP AF
LD (FPTR),A
RET

BATTI: BATTERIETEST INTERN
PUSH DE
LD H,0
LD B,16 MESSUNGEN

BT_01:
PUSH BC
PUSH HL
CALL TIMER
LD D,0
LD E,A
POP HL
ADD HL,DE
POP BC
DJNZ BT_01

LD B,4 DURCH 16 TEILEN

BT_02:
SRL H
LD A,L
RRA
LD L,A
DJNZ BT_02

PUSH HL
POP DE
LD HL,0
ADC HL,DE
ADC HL,DE
ADC HL,DE
POP DE
SCF
CCF
SBC HL,DE

RET

INFO:   INFO-AUSGABE
PUSH HL
PUSH DE
PUSH BC

CALL CLS

LD A,(QUELLE)
CP "C"
JP Z,INF_03

'LAUFWERK UND FREIER
'SPEICHER AUF LAUFWERK

CALL DSKF
PRINTAT &0000,ANZERG
LD A,(QUELLE)
CALL Q_TEXT
CALL PRTSTR
PRINT ERGEB-1
PRINT ANZBYT

'FILEMASKE
PRINTAT &0101,MI_TXT
LD B,8
LD DE,FMASK

INF_01:
LD A,(DE)
CALL PRTAKKU
INC DE
DJNZ INF_01

LD A,"."
CALL PRTAKKU
LD B,3

INF_02:
LD A,(DE)
CALL PRTAKKU
INC DE
DJNZ INF_02

'ANZAHL FILES UND ANZAHL
'MARKIERTER FILES ()

PRINTAT &0102,AF_TXT
LD A,(COUNT)
LD L,A
LD H,0
LD DE,BSTR B$
CALL &0283
XOR A
LD (DE),A
PRINT BSTR

LD A,"("
CALL PRTAKKU

LD A,(MARKED)
LD L,A
LD H,0
LD DE,BSTR B$
CALL &0283
XOR A
LD (DE),A
PRINT BSTR

LD A,")"
CALL PRTAKKU

INF_03:
'DATUM UND UHRZEIT
CALL TIME
PRINTAT &0103,BSTR

'BATTERIESPANNUNG INTERN
PRINTAT &0E03,POWERI
LD DE,528
LD C,&18
CALL BATTI
LD DE,4
SBC HL,DE
LD DE,BSTR
CALL &0283
XOR A
LD (DE),A
PRINT BSTR
LD A,"%"
CALL PRTAKKU
LD A,(&F0AE)
CP &10
JR Z,INF_EX

PRINT POWERE
LD DE,550
LD C,&1A
CALL BATTI
LD DE,7
SBC HL,DE
LD DE,BSTR
CALL &0283
XOR A
LD (DE),A
PRINT BSTR
LD A,"%"
CALL PRTAKKU

INF_EX:
CALL INKEY
CALL CLS

POP BC
POP DE
POP HL
RET

TIME:
'ermitteln von Datum und
'Uhrzeit. Eintrag in B$
'beendet mit &00
LD HL,ASTR A$
LD C,TMREAD
CALL TIMER

LD DE,BSTR
LD HL,ASTR+1  TAG-BCD
LD A,(HL)
CALL BCDASC
LD A,"."
LD (DE),A
INC DE
DEC HL

PUSH HL   MONAT-BINAER
LD L,(HL)
LD H,0
CALL STRCNV
POP HL
LD A,"."
LD (DE),A
INC DE
LD A," "
LD (DE),A
INC DE
INC HL
INC HL     STUNDE-BCD
LD A,(HL)
CALL BCDASC
LD A,":"
LD (DE),A
INC DE
INC HL     MINUTE-BCD
LD A,(HL)
CALL BCDASC
LD (DE),A
'INC DE
XOR A
LD (DE),A
RET

BCDASC:
PUSH AF
AND &F0
RRCA
RRCA
RRCA
RRCA
CALL BINASC
LD (DE),A
INC DE
POP AF
AND &0F
CALL BINASC
LD (DE),A
INC DE
RET

KILL: DATEIEN LOESCHEN
LD A,(COUNT)
OR A
RET Z  KEIN FILE IN LISTE
LD A,(MARKED)
OR A
JP Z,KLWLD  KILL MIT WILDCARDS

CALL SV_FMSK FMASK SICHERN
CALL MENLIN
LD DE,BES_TXT
CALL PRINT

CALL YESNO
RET Z
JR NC,KL_01  NEIN

LD A,&01     JA
LD (BES_TAG),A
JR KL_02

KL_01:       NEIN
XOR A
LD (BES_TAG),A

KL_02:
CALL MENLIN
LD DE,DEL_TX
CALL PRINT
XOR A

KL_03:
LD B,A
LD A,(COUNT)
SUB A,B
JR Z,DEL_EX
LD A,B
INC A
PUSH AF
CALL GFNADR
POP AF
DEC HL
LD B,A
XOR A
OR (HL)  FILE MARKIERT?
LD A,B
JR Z,KL_03 NEIN

CALL SHOWDIR

CALL ASK_YN JA NEIN FRAGEN
JR NC,KL_04

PUSH AF
LD DE,&0F00
CALL CRSRSET
LD DE,JANEIN
CALL PRINT
POP AF

CALL YESNO
JR Z,DEL_E1
JR NC,KL_03   NEIN

KL_04:
CALL DEL_FL
JR C,DEL_E1
CALL MARK
JR KL_03

DEL_EX:
CALL SETWLD
CALL SV_FMSK
CALL RDSRC NEU EINLESEN
JR NC,DEL_E1
CALL CLS

DEL_E1:
CALL LD_FMSK
LD DE,&0E00
CALL CRSRSET
LD B,14
JP BLANKS

DEL_FL:  FILE LOESCHEN
'        FILE IN A
PUSH HL
PUSH DE
PUSH BC
PUSH AF
LD DE,FCB
LD C,&1A    FCB ANLEGEN
CALL FILE
JR C,DF_ER

DF_01:
POP AF
PUSH AF  A=FILENR
CALL SETFNAME
CALL SETFCB
LD C,&13
CALL FILE
JR NC,DF_EX

DF_ER:
CALL FEHLER
POP AF
SCF
JR DF_E1

DF_EX:
POP AF
SCF
CCF

DF_E1:
POP BC
POP DE
POP HL
RET

SETFNAME:  FILENAME SETZEN
'          IN FMASK 
'          FILENR IN A
PUSH HL
PUSH DE
PUSH BC
PUSH AF
CALL GFNADR
LD DE,FBFLEN
SCF
CCF
SBC HL,DE    HL=FILENAMEADR
            'QUELLE
LD DE,FMASK 'ZIEL
LD BC,&000C 'ANZAHL
LDIR

SFN_EX:
POP AF
POP BC
POP DE
POP HL
RET

ASK_YN:
PUSH AF
LD A,(BES_TAG)
OR A
JR Z,NO
JR YES

YESNO:  NUR (J)A UN (N)EIN
'   ALLE REGISTER BLEIBEN 
'   ERHALTEN
'   YES  = SC, NZ
'   NO   = NC, NZ
'   MODE = NC, SZ
PUSH AF

YN_01:
CALL INKEY2
CP "J"
JR Z,YES
CP "N"
JR Z,NO
CP MODE
JR Z,NOT
JR YN_01

NO:
POP AF
OR A
RET

YES:
POP AF
SCF
RET

NOT: MODE GEDRUECKT
POP AF
XOR A
OR A
RET

SV_FMSK:  FILEMASK SPEICHERN
PUSH HL
PUSH DE
PUSH BC
LD HL,FMASK
LD DE,FMASK1
LD BC,FNLEN
LDIR
POP BC
POP DE
POP HL
RET

LD_FMSK:  FILEMASK LADEN
PUSH HL
PUSH DE
PUSH BC
LD HL,FMASK1
LD DE,FMASK
LD BC,FNLEN
LDIR
POP BC
POP DE
POP HL
RET

KLWLD:
'KILL MIT WILDCARDS

CALL MENLIN
LD HL,KLTX01
CALL PRINT
LD DE,&0603
LD BC,&0600
CALL INPUT
RET C

LD HL,ASTR A$
LD A,(HL)
CP CR
RET Z

LD DE,KWTX01
CALL PRINT
CALL YESNO
RET Z
RET NC

LD HL,ASTR A$
LD A,(HL)
CP CR
RET Z

CALL SETFCB

LD DE,FCB+&09
LD B,&08
CALL NWFNS

LD DE,FCB
LD C,&13
CALL FILE
JR NC,KW_01
JP FEHLER

KW_01:
CALL SETWLD
JP RDSRC

DSKF: GET FREE SPACE ON DISK
CALL SETFCB
LD DE,FCB
LD C,&1B
CALL XFILE 
OR A
JR Z,DSKF02
LD A,(&F89B)
JP FEHLER

DSKF02:
PUSH BC BYTES PRO SEKTOR
LD D,&00
PUSH DE SEKT PRO CLUSTER
PUSH HL FREE CLUSTER

POP DE
CALL &024A BIN2BCD
LD HL,&FA00
LD DE,&FA10
LD BC,&0008
LDIR        XX->YY

POP DE      SEKT/CLUST
CALL &024A  BIN2BCD
LD A,&02
LD (&F88C),A
CALL &0220   XX*YY->XX
LD HL,&FA00
LD DE,&FA10
LD BC,&0008
LDIR        XX->YY

POP DE      BYTES/SEKT
CALL &024A  BIN2BCD
LD A,&02
LD (&F88C),A
CALL &0220   XX*YY->XX

LD HL,&FA00
LD DE,ERGEB
JP &0244

FORMAT: DISKETTE FORMATIEREN
CALL ISDISK
JR C,FRM_02
LD A,155
JP FEHLER

FRM_02:
CALL MENLIN
LD DE,FRMTTX
CALL PRINT
LD DE,&0003
LD A,26
CALL RVSCHR
CALL YESNO
JR C,FRM_01
RET

FRM_01:
LD A,&01  X:
LD C,&83
RST &20
DEFB &05
DEFW &4008
RET NC

DSK_ER:  FEHLER BEI DIREKT
'        AUFRUF DISKROM
CP &08
JR Z,ER_NF  NO FORMAT
CP &20
JR Z,ER_WP  WRITEPROTECT
CP &40
JR Z,ER_AE  ACCU EMPTY
CP &80
JR Z,ER_ND  NO DISK

ER_OUT:
JP FEHLER

ER_NF:
LD A,161
JR ER_OUT

ER_WP:
LD A,159
JR ER_OUT

ER_AE:
LD A,168
JR ER_OUT

ER_ND:
LD A,160
JR ER_OUT

DATELN: DATUM UND LAENGE
'       KOPIEREN
PUSH BC
PUSH HL
PUSH DE
LD BC,&0010
ADD HL,BC
LD A,(HL)
INC HL
LD H,(HL)
LD L,A

LD B,&00
LD A,H
CP &28
JR NC,DL_04
CP &27
JR NZ,DL_02
LD A,L
CP &10
JR NC,DL_04

DL_02:
INC B
LD A,H
CP &04
JR NC,DL_01
CP &03
JR NZ,DL_03
LD A,L
CP &E8
JR NC,DL_01

DL_03:
INC B
LD A,H
CP &01 
JR NC,DL_01
OR A
JR NZ,DL_05
LD A,L
CP &64
JR NC,DL_01

DL_05:
INC B
CP &0A
JR NC,DL_01
INC B

DL_01:
LD A," "
LD (DE),A
INC DE
DJNZ DL_01

DL_04:
CALL &0283
POP DE
EX DE,HL
LD BC,&0005
ADD HL,BC
XOR A
LD (HL),A

POP HL
POP BC
RET

FRMTTX:
DEFB "Diskette X: formatieren?"
DEFB &00

ANZERG:
DEFB " Free on "
DEFB 0

DEFB ":"
ERGEB:
DEFS 7

AF_TXT:
DEFB "Anzahl Files = "
DEFB 0

ANZBYT:
DEFB " Bytes"
DEFB &00

KLTX01:
DEFB "KILL "
DEFB &22
DEFB &00

KWTX01:
DEFB " KILL?"
DEFB &00

FMASK1:
DEFS FNLEN

DEL_TX:
DEFB "L”sche..."
DEFB &00

BES_TXT:
DEFB "einzeln best„tigen"

JANEIN:
DEFB " [J/N]:"
DEFB &00

BES_TAG:
DEFB &00

LOE_TXT:
DEFB "l”schen?"
DEFB &00

POWERI:
DEFB "I="
DEFB 0

POWERE:
DEFB ",E="
DEFB 0

