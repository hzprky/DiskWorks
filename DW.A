'|DW.A DiskWorks
'|     created:
'| (c) 1992 by KiKiSoft
'+-----------------------
'------ PROGRAMMANFANG

AUTO    ASTART

ASTART: START NACH LADEN
LD SP,MSTACK
CALL INIT

IFD PUBLIC
  CALL LOGO
ENDC

CALL RDSRC
CSTART: START NACH CALL
CALL CLS

M_L0: MENUE-ANFANG
CALL MENUE

M_LOOP:
LD A,(FPTR)
CALL SHOWDIR
PRINTAT 0,ARROW

MEN_01:
CALL INKEY

CP DEF  CHANGE LAYER
JR NZ,M_L01
LD A,(LAYER)
CPL
LD (LAYER),A
JP M_L0

M_L01:
CP MODE
JR NZ,M_LAA
IFD GERNOT
LD DE,GERSTA 
LD A,(GERLEN)
CALL &016C
ENDC
JP EXIT

M_LAA:
CP CUP
JR NZ,M_L02
LD A,(FPTR)
DEC A
CP &00
JR Z,M_LOOP
LD (FPTR),A
JP M_LOOP

M_L02:
CP CDN
JR NZ,M_L03
LD A,(FPTR)
LD HL,COUNT
CP (HL)      GROESSER?
JP NC,M_LOOP
INC A
LD (FPTR),A
JR M_LOOP

M_L03:
CP " "
JR NZ,M_L04
LD A,(FPTR)
CALL MARK
JR M_LOOP

M_L04:
CP CLEFT
JR NZ,M_L05
LD A,&01
LD (FPTR),A
JR M_LOOP

M_L05:
CP CRIGHT
JR NZ,M_L06
LD A,(COUNT)
LD (FPTR),A
JP M_LOOP

M_L06:
CP RCL
JR NZ,M_L07
CALL MARKALL
JP M_LOOP

M_L07:
CP CR
JR NZ,M_L21
CALL RUNIT
JP M_L0

M_L21:
CP KB2
JP NZ,M_L08
CALL SWAP
JP M_L0

M_L08:
PUSH AF  ASK USERDEF KEYS
CALL CFGKEY
POP AF

LD B,A
LD A,(LAYER)
OR A
LD A,B
JP NZ,M_L15

CP F1
JR NZ,M_L09
CALL INFO
JP M_L0

M_L09:
CP F2
JR NZ,M_L10
CALL COPY
JP M_L0

M_L10:
CP F3
JR NZ,M_L11
CALL KILL
JP M_L0

M_L11:
CP F4
JR NZ,M_L12
CALL IMASK
JP M_L0

M_L12:
CP F5
JR NZ,M_L13
CALL C_QUELL
JP M_L0

M_L13:
CP F6
JP NZ,M_LOOP
CALL C_ZIEL
JP M_L0

'ZWEITE MENUE-EBENE
M_L15:
CP F1
JR NZ M_L16
CALL NAME
JP M_L0

M_L16:
CP F2
JR NZ,M_L17
CALL PROTECT
JP M_L0

M_L17:
CP F3
JR NZ,M_L18
CALL VIEW
JP M_L0

M_L18:
CP F4
JR NZ,M_L19
CALL PARAM
JP M_L0

M_L19:
CP F5
JR NZ,M_L20
CALL FORMAT
JP M_L0

M_L20:
CP F6
JP NZ,M_LOOP
CALL DISKCP
JP M_L0

EXIT: PROGRAMM BEENDEN
LD A,(BANK)
OUT (&31),A
CALL CLS

CALL &0312  SET BASIC PARM
JP &0253    BACK TO BASIC

INIT: INITIALISIEREN
IN A,(&31)
LD (BANK),A

LD A,"2" SRC="S2:"
LD (QUELLE),A

CALL &0175
CALL ISDISK
LD A,"X" ZIEL="X:"
JR C,INIT01   ->DISK DA
LD A,"C" ZIEL="COM:"

INIT01: 
LD (ZIEL),A
LD HL,DIRMEM
LD (DMPTR),HL
XOR A
LD (COUNT),A
LD (LAYER),A
LD (MARKED),A
INC A
LD (FPTR),A

LD B,&00
CALL SMBLRD
RES 0,A      BUSY
CALL SMBLSET
LD B,&02
CALL SMBLRD
RES 1,A   CTRL
RES 3,A   S
RES 7,A   KBII
CALL SMBLSET

CALL LOADCFG
JR C,EXIT

SETWLD:  WILDCARD SETZEN
PUSH HL
PUSH DE
PUSH BC
PUSH AF
LD HL,FMASK
LD DE,FMASK+1
LD A,WLDCRD
LD (HL),A
LD BC,FNLEN-1
LDIR
CALL SV_FMSK
POP AF
POP BC
POP DE
POP HL
RET

GFNADR: GET FILENAME ADDRESS
'ERMITTELT DIE ADRESSE DES
'AUF A FOLGENDEN FILES
PUSH BC
LD HL,DIRMEM-FBFLEN
LD DE,FBFLEN
INC A
LD B,A
MK_01:
ADD HL,DE
DJNZ MK_01
POP BC
RET

MARK: AKTUELLE DATEI
'     MARKIEREN ODER
'     DEMARKIEREN
'     DATEI IN A
PUSH HL
PUSH DE
PUSH BC
PUSH AF
CALL GFNADR
DEC HL
'HL=MARKIERUNGSSTELLE

LD A,(HL)
OR A
JR Z,MK_EINS
XOR A
LD (HL),A
LD HL,MARKED
DEC (HL)
JR MK_EX

MK_EINS:
LD A,&01
LD (HL),A
LD HL,MARKED
INC (HL)
MK_EX:
POP AF
POP BC
POP DE
POP HL
RET

MARKALL: ALLES MARKIEREN
'        ODER DEMARKIEREN
PUSH AF
PUSH BC
LD A,(COUNT)
LD B,A
LD A,&01
MA_1:
CALL MARK
INC A
DJNZ MA_1
MA_EX:
POP BC
POP AF
RET

PRINT: STRING AUSGEBEN
PUSH AF
PUSH DE
CALL PRTSTR
POP DE
POP AF
RET

'----------
IFD PUBLIC
PRTLST: MEHR ALS EINE ZEILE
PUSH AF
PUSH BC
PUSH HL
LD HL,&00FF
LD (&F05F),HL
LD B,&04

PL_01:
CALL LONGP
DJNZ PL_01
POP HL
POP BC
POP AF
RET

LONGP:
PUSH AF
PUSH BC
PUSH HL
LD HL,(&F05F)
LD A,L
CP &03
JR Z,PD_01
INC L
JR PD_02

PD_01:
CALL UPSCRL

PD_02:
LD H,&00
LD (&F05F),HL
CALL PRTSTR
POP HL
POP BC
POP AF
RET

ENDC
'----------
INKEY2: MIT BLINKCURSOR
LD A,2
JR IN_06

INKEYC: MIT UNDERSCORE
LD A,1
JR IN_06

INKEY:  BLINDE EINGABE
XOR A

IN_06:
PUSH DE
PUSH BC
CALL CRSRSTA

IN_01:
CALL KEYGET
CP &01
JR NZ,IN_04
LD B,&00
CALL SMBLRD
XOR &02
CALL SMBLSET
JR IN_01

IN_04:
CP &02
JR NZ,IN_05
LD B,&00
CALL SMBLRD
XOR &08
CALL SMBLSET
JR IN_01

IN_05:
CP KOFF
JR NZ,IN_02
CALL OFF
JR IN_01

IN_02:
PUSH AF
LD B,&00
CALL SMBLRD
RES 1,A
CALL SMBLSET
POP AF
POP BC
POP DE
RET

UPCASE:
CP "a"
JR C,UC_01
CP "{"
JR NC,UC_01
SUB A,&20

UC_01:
RET

'-----------
IFD PUBLIC

WAIT: EIN PAAR SEKUNDEN...
LD BC,WAIT_1
W_LP1:
LD DE,WAIT_2
W_LP2:
DEC DE
LD A,D
OR E
JR NZ,W_LP2
DJNZ W_LP1
RET

LOGO: LOGO AUSGEBEN
CALL CLS
CALL HOME
LD DE,L_TXT
CALL PRTLST
JP WAIT
ENDC

'------------
MENUE: MENUE ANZEIGEN
PUSH DE
PUSH AF
LD A,(BANK)
OUT (&31),A
CALL MENLIN
LD A,(LAYER)
OR A
JR NZ,MN2_01

LD DE,M_TXT1
CALL PRINT

LD DE,&1103
CALL CRSRSET
LD A,(QUELLE)
CALL Q_TEXT
CALL PRINT
LD A,":"
CALL PRTAKKU
LD DE,&1503
CALL CRSRSET
LD A,(ZIEL)
CALL Q_TEXT
CALL PRINT
LD A,":"
CALL PRTAKKU
JR MEN_EX

MN2_01:
LD DE,MN2_TX
CALL PRINT

MEN_EX:
LD DE,&0003
LD A,26
CALL RVSCHR
POP AF
POP DE
RET

C_QUELL:QUELLE AENDERN
CALL MENLIN
LD DE,Q_TXT
CALL PRINT
LD A,(QUELLE)
LD (Q_BUFF),A
CALL Q_IN
CP MODE
RET Z

LD (QUELLE),A
CALL SETWLD
CALL RDSRC
JR NC,CQ_02

LD A,(&F89B)
CP &98
LD A,0
LD (COUNT),A
JR Z,CQ_02

LD A,(Q_BUFF)
LD (QUELLE),A
CALL RDSRC

CQ_02:
CALL CLS
LD A,&01    VOM ERSTEN AN
JP SHOWDIR

Q_IN: ZEICHEN EINLESEN
CALL INKEY
CP MODE
RET Z
CP F2
JR Z,QI_C1
CP F3
JR Z,QI_X
CP F4
JR Z,QI_Y
CP F5
JR Z,QI_1
CP F6
JR Z,QI_2
JR Q_IN

QI_X:
LD A,"X"
RET

QI_Y:
LD A,"Y"
RET

QI_1:
LD A,"1"
RET

QI_2:
LD A,"2"
RET

QI_C1:
LD A,"C"
RET

SHOWDIR: FILES ANZEIGEN
'        START IN A
'        3 WERDEN GEZEIGT
'        IN DEN ERSTEN 3
'        ZEILEN DES DISPLAY
'        ES MUSS SICHERGESTELLT
'        DASS START<ANZAHL IST
PUSH AF
PUSH BC
PUSH DE
PUSH HL

LD (BUFF),A   MERKEN
LD B,A

LD A,(COUNT)
CP &00
JR Z,SD_EX

'START ERMITTELN
LD HL,DIRMEM-FBFLEN
LD DE,FBFLEN

SD_1:
ADD HL,DE
DJNZ SD_1
'(HL)=ERSTER DATEINAME
LD (AKTPTR),HL

XOR A    ZEILE 0(1)
LD (LINE),A

SD_2:
LD D,2   X=2
LD E,A   Y=AKKU
PUSH AF
CALL CRSRSET
POP AF
LD DE,(AKTPTR) DE=AKT FILENAME
CALL SHOWFL
CALL SHOWMK

LD A,(BUFF)
INC A
LD (BUFF),A
LD C,A
LD A,(COUNT)
CP C        GROESSER?
JR C,SD_EX  JA!

LD HL,(AKTPTR)
LD DE,FBFLEN
ADD HL,DE
LD (AKTPTR),HL

LD A,(LINE)
INC A
LD (LINE),A
CP 3
JR C,SD_2

SD_EX:
LD A,(LINE)
CP &00
JR NZ,SD_03
LD A,1
CALL DEL1LIN
LD A,2
CALL DEL1LIN
JR SD_04

SD_03:
CP &01
JR NZ,SD_04
LD A,2
CALL DEL1LIN

SD_04:
POP HL
POP DE
POP BC
POP AF
RET

SHOWFL:    FILE ANZEIGEN
PUSH AF
PUSH BC
PUSH DE
LD B,&08

SFI_01:[9]   NAME
LD A,(DE)
CALL PRTAKKU
INC DE
DJNZ SFI_01

LD A,"."
CALL PRTAKKU

LD B,&03

SFI_02:
LD A,(DE)
CALL PRTAKKU
INC DE
DJNZ SFI_02

SFI_03:
LD A,(DE)
OR A
JR Z,SFI_04
CALL PRTAKKU
INC DE
JR SFI_03

SFI_04:
POP DE
POP BC
POP AF
RET 

SHOWMK: ZEIGE MARKIERUNG
PUSH HL
PUSH DE
PUSH AF
EX DE,HL
LD DE,FBFLEN-1
ADD HL,DE
LD A,(HL)
OR A
JR Z,SM_1
LD A,(LINE)
LD E,A
LD D,2
LD A,18
CALL RVSCHR

SM_1:
POP AF
POP DE
POP HL
RET

MENLIN:  MENUEZEILE
'        LOESCHEN
PUSH AF
PUSH DE
LD A,&03
CALL DEL1LIN
LD DE,&0003
CALL CRSRSET
POP DE
POP AF
RET

'--------------------
'   DATENSPEICHER
'--------------------

BANK: SPEICHERBANK
DEFB 0

QUELLE: QUELLAUFWERK
DEFB "2"

ZIEL:   ZIELLAUFWERK
DEFB "X"

Q_BUFF: BUFFER FUER QUELLE
DEFB &00

'TEXTE
IFD PUBLIC

L_TXT: LOGO-TEXT
DEFB "ษออออออออออออออออออออออออป"
DEFB "บ     DiskWorks V1.0     บ"
DEFB "บ  (c) 1993 by KiKiSoft  บ"
DEFB "ศออออออออออออออออออออออออผ"

ENDC

ZGQ_TX:
DEFB "98 Ziel = Quelle!"
DEFB &00

M_TXT1:
DEFB " INF CPY KIL MSK"
DEFB 0

MN2_TX:
DEFB " NAM PRO VIE PAR INI DCP"
DEFB &00

BUFF:
DEFB &00

LINE:
DEFB &00

AKTPTR:
DEFW &0000

FPTR:  FILEPOINTER (ANZEIGE)
DEFB &00

COUNT: FILEZAEHLER
DEFB &00

LAYER: MENUEEBENE
DEFB &00

DMPTR: FILENAMEPOINTER
DEFW &0000

MARKED: ANZAHL MARKIERTER FILES
DEFB &00

X_TXT:
DEFB "X"
DEFS 3

Y_TXT:
DEFB "Y"
DEFS 3

S1_TXT:
DEFB "S1"
DEFS 2

S2_TXT:
DEFB "S2"
DEFS 2

C1_TXT:
DEFB "COM"
DEFS 1

ARROW:
DEFB "->"
DEFB &00

Q_TXT:
DEFB "SRC: COM  X:  Y: S1: S2:"
DEFB 0

FMASK: DATEIMASKE
DEFS FNLEN

IFD GERNOT
GERLEN:
DEFB 18

GERSTA:
DEFB "LOAD"
DEFB &22
DEFB "S2:ST.BAS"
DEFB &22
DEFB ",R"
DEFB &0D
ENDC

MI_TXT: EINGABEFELD DATEIMASKE
DEFB "Filemaske:"
DEFB &00

DIRMEM:

END
