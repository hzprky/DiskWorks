'|NA.A diverse Funktionen
'|     aus DiskWorks
'| (C) 1992 by KiKiSoft
'+-----------------------
CFG:
DEFB "DW      CFG"
DEFB 0

LOADCFG: QUICKSTART CFG-
'DATEN LADEN AUS DW.CFG

'ERST BEREICH L™SCHEN
LD HL,CFGDATA
LD DE,CFGDATA+1
LD BC,START-CFGDATA-1
LD (HL),&00
LDIR

LD HL,CFG
LD DE,FMASK
LD BC,FNLEN
LDIR
CALL OPENFF
RET C
LD HL,CFGDATA-&0100

LCF_01:
LD DE,&0100
ADD HL,DE
LD (TRANAD),HL
LD C,RD_FL
LD DE,FCB
CALL FILE
JR NC,LCF_01

CP &A2
JR Z,LCF_02
CALL CLOSE1
JP FEHLER

LCF_02:
CALL CLOSE1

'CFG-FILE GELESEN
'JETZT VERARBEITEN
'DATEN STEHEN AB TRDATA
RET

CFGKEY: USERDEFINED KEYS
LD B,A A=TASTE
LD HL,CFGDATA-1

CFGK01:
INC HL
LD A,(HL)
CP TRENNER
RET NZ

CFGK03:
INC HL
LD A,(HL)
CP B =TASTE?
JR Z,EXEC

CFGK02:
INC HL
LD A,(HL)
CP LF
JR Z,CFGK03
CP TRENNER
RET Z
JR CFGK02

EXEC: KOMMANDO AN BS 
'šBERGEBEN (KEYBUFF$)
INC HL
INC HL
PUSH HL
POP DE
LD C,0

EXC_02:
LD A,(HL)
CP "~"
JR NZ,EXC_03
LD A,CR
LD (HL),A
JR EXC_04

EXC_03:
CP CR
JR Z,EXC_01
CP TRENNER
JR Z,EXC_01

EXC_04:
INC HL
INC C
JR EXC_02

EXC_01:
LD A,C
CALL &016C
JP EXIT

NAME: DATEIEN UMBENENNEN
LD A,(COUNT)
OR A
RET Z
LD A,(MARKED)
OR A
JP Z,NAMWLD

CALL SV_FMSK

XOR A

NM_01:
LD B,A
LD A,(COUNT)
SUB A,B
JR Z,NM_EX1

LD A,B
INC A
PUSH AF
CALL GFNADR
POP AF
DEC HL
LD B,A
XOR A
OR (HL)
LD A,B
JR Z,NM_01  NŽCHSTER

PUSH AF     RENAME
CALL SHOWDIR
CALL MENLIN
LD HL,NM_TXT
LD DE,&0803
LD BC,&0800
CALL INPUT
JR C,NM_EX

LD HL,ASTR A$
LD A,(HL)
CP CR
JR Z,NM_EX

POP AF
PUSH AF
DEC A
CALL GFNADR HL=QUELLFILE
LD DE,FMASK
LD BC,FNLEN
LDIR
CALL SETFCB

'ERST 'AS-NAME' BLANKEN
LD HL,FCB+&29
LD DE,FCB+&2A
LD BC,FNLEN-1
LD A," "
LD (HL),A
LDIR

LD HL,ASTR A$
LD DE,FCB+&29
CALL SETFN
JR C,NM_EX

CALL COMPHD
JR C,NM_EX

'UND FUNKTION DURCHFšHREN
LD C,NM_FL
LD DE,FCB
CALL FILE
JR NC,NM_02

'FEHLER AUSGEBEN
CALL FEHLER
JR NM_EX

NM_02:
POP AF
CALL MARK DEMARKIEREN
JP NM_01  UND NŽCHSTER

NM_EX:  DAS WAR'S
POP AF

NM_EX1:
CALL LD_FMSK
LD A,(FPTR)
PUSH AF
CALL RDSRC
POP AF
LD (FPTR),A
RET

COMPHD:
LD HL,FCB+&09
LD DE,FCB+&29
LD B,FNLEN-1
CALL CPHLDE
SCF
CCF
RET NZ
LD A,&97
JP FEHLER

BEEP: PIEPER AUSGEBEN
PUSH AF
PUSH BC
LD A,&40
LD BC,&00FF
CALL &01B7
POP BC
POP AF
RET

CPHLDE: (HL) UND (DE) VERGL.
LD A,(DE)
CP (HL)
RET NZ
INC DE
INC HL
DJNZ CPHLDE
RET

NAMWLD:
'RENAME MIT WILDCARDS

CALL CLS
LD DE,&0003
CALL CRSRSET
LD HL,NMTX01
LD DE,&0703
LD BC,&0600
CALL INPUT
JR C,NW_02

LD HL,ASTR A$
LD A,(HL)
CP CR
RET Z

CALL SETFCB

LD DE,FCB+&09
LD B,&08
CALL NWFNS

CALL UPSCRL

LD DE,&0003
CALL CRSRSET
LD HL,NMTX02
LD DE,&0603
LD BC,&0600
CALL INPUT
JR C,NW_02

LD HL,ASTR A$
LD A,(HL)
CP CR
JR Z,NW_02

LD DE,FCB+&29
CALL NWFNS

CALL COMPHD
JR C,NW_02

LD DE,FCB
LD C,NM_FL
CALL FILE
JR NC,NW_02

'FEHLER AUSGEBEN
CALL FEHLER

NW_02:
CALL NM_EX1
LD A,2
JP DEL1LIN

NWFNS:
'SET FILENAME FUER RENAME
'MIT WILDCARDS
LD B,&08
CALL SETFN
LD A,(HL)
CP "."
JR NZ,NF_01
INC HL

NF_01:
LD B,&03
JP SETFN

RUNIT:
'STARTEN VON PROGRAMMEN
LD A,(COUNT)
OR A
RET Z
CALL SV_FMSK

LD A,(FPTR)
DEC A
CALL GFNADR  ADRESSE>HL
LD (&F1B7),HL  MERKEN
LD DE,FMASK
LD BC,FNLEN
LDIR
CALL OPENFF  ™FFNEN
JP C,RI_10

LD HL,TRDATA  TRANSFERADR
LD (TRANAD),HL

LD C,RD_FL   READ
LD DE,FCB    1st BLOCK
CALL FILE
PUSH AF
CALL CLOSE1
POP AF
JP C,RI_10

LD A,(TRDATA)
CP &FF       BASIC ODER ML?
JR NZ,RI_10  NEIN>DANN ZURšCK

CALL MENLIN
LD DE,RNTX03
CALL PRINT
CALL YESNO
JR C,RI_08
JR RI_10

RI_08:
LD A,(QUELLE)
CALL Q_TEXT
EX DE,HL
LD DE,RNTX02
LD C,&06

RI_07:
LD A,(HL)
CP &00
JR Z,RI_06
LD (DE),A
INC DE
INC HL
INC C
JR RI_07

RI_06:
LD A,":"
LD (DE),A
INC C
INC DE

LD HL,(&F1B7) FILENAME HOLEN
LD B,&08

RI_01:
LD A,(HL)
INC HL
CP " "
JR Z,RI_02

LD (DE),A
INC C
INC DE

RI_02:
DJNZ RI_01

LD A,"."
LD (DE),A
INC C
INC DE

LD B,3

RI_03:
LD A,(HL)
INC HL
CP " "
JR Z,RI_04

LD (DE),A
INC C
INC DE
DJNZ RI_03

RI_04:
LD A,(&F404)
CP &10
JR Z,MLPRO

BASPRO:
LD A,&22
LD (DE),A
INC DE
LD A,","
LD (DE),A
INC DE
LD A,"R"
LD (DE),A

INC DE
LD A,CR
LD (DE),A
LD A,C
ADD A,3

LD DE,RNTX04
JR RI_09

MLPRO:
LD A,CR
LD (DE),A
INC C
LD A,C
LD DE,RNTX01

RI_09:
CALL &016C
JP EXIT

RI_10:
JP LD_FMSK

CHDEV:  FCB2 MIT FUER OUTPUT
PUSH AF
PUSH BC
PUSH DE
PUSH HL

LD A,(ZIEL) ZIEL LESEN
CALL Q_TEXT

LD HL,FCB2+1  FUELLEN
LD B,&04

SF2_01:
LD A,(DE)
LD (HL),A
INC DE
INC HL
DJNZ SF2_01

POP HL
POP DE
POP BC
POP AF
RET

COMCOP: KOPIEREN VON DEM
'       SERIELLEN PORT
CALL MENLIN
LD HL,NM_TXT
LD DE,&0309
CALL INPUT
RET C
LD HL,ASTR
LD A,(HL)
CP CR
RET Z

LD HL,ASTR
LD DE,FMASK
CALL SETFN
RET C

CALL CP_FILE
RET NC
JP FEHLER

COPY:
'KOPIEREN VON DATEIEN
'DIE MARKIERT WURDEN
'WILDCARDS SIND NICHT
'VORGESEHEN
LD A,(QUELLE)
CP "C"
JR Z,COMCOP

LD A,(COUNT)
OR A
RET Z
LD A,(MARKED)
OR A
RET Z

CALL SV_FMSK

CALL MENLIN
LD A,(QUELLE)
LD B,A
LD A,(ZIEL)
CP B
JR NZ,CP_04

LD DE,ZGQ_TX
JP FEAUS

CP_04:
LD DE,CPTX01
CALL PRINT
CALL YESNO
JR C,CP_03
RET

CP_03:
XOR A

CP_01:
LD B,A
LD A,(COUNT)
SUB A,B
JR Z,CP_EX1

LD A,B
INC A
PUSH AF
CALL GFNADR
POP AF
DEC HL
LD B,A
XOR A
OR (HL)
LD A,B
JR Z,CP_01  NŽCHSTER

CALL SHOWDIR
PUSH AF
DEC A
CALL GFNADR HL=QUELLFILE
LD DE,FMASK
LD BC,FNLEN
LDIR        FILENAME SET

CALL CP_FILE
JR C,CP_EX

POP AF
CALL MARK DEMARKIEREN
JR CP_01  UND NŽCHSTER

CP_EX:  DAS WAR'S
POP AF

CP_EX1:
JP LD_FMSK

CP_FILE:
'FUNKTION ZUM KOPIEREN
LD A,(ZIEL)
CP "C"  SERIELL?
JR NZ,CPF_04

CALL MENLIN
PRINT SERTXT
CALL YESNO
JR C,CPF_04
RET

CPF_04:
LD HL,TRDATA   TRANSFERADR
LD (TRANAD),HL EINTRAGEN
CALL OPENFF
RET C

CALL MENLIN

CPF_02:
LD A,(QUELLE)
CP "C"
JR Z,CPF_30

LD HL,FCB+&09
LD DE,FCB2+&09
LD BC,&1A
LDIR
JR CPF_31

CPF_30:
LD HL,FMASK
LD DE,FCB2+9
LD BC,FNLEN
LDIR

CPF_31:
CALL CHDEV  UND DEVICE ŽNDERN

LD C,CR_FL  CREATE
LD DE,FCB2  2nd FILE
LD A,&01
LD (DE),A
CALL FILE   NOW
JR C,CPF_ER

CPF_01:
PUSH AF  FORTSCHRITT ANZ
LD A,"."
CALL PRTAKKU
POP AF

LD C,RD_FL  READ
LD DE,FCB   1st FILE
CALL FILE
JR NC,CPF_03

CP &A2
JR Z,CPF_EX

LD A,B
LD A,(QUELLE)
CP "C"
JR NZ,CPF_ER
LD A,B
OR A
JR Z,CPF_EX

'FEHLER AUSGEBEN
JR CPF_ER

CPF_03:
LD C,WR_FL    WRITE
LD DE,FCB2  2nd FILE
CALL FILE
PUSH AF FORTSCHRITT ANZ.
LD A,(CURX)
DEC A
LD (CURX),A
LD A,"o"
CALL PRTAKKU
POP AF
JR NC,CPF_01

CPF_ER:
'FEHLER AUSGEBEN

LD B,A
CALL CLOSEALL ALLES ZU
LD DE,FCB2  ...NACHFILE
LD C,DL_FL    L™SCHEN
CALL FILE
LD A,B
CALL FEHLER
SCF
JR CPF_E1

CPF_EX:
LD A,(FCB+&25)  LŽNGE
LD (FCB2+&06),A

LD A,(QUELLE)
CP "C"
JR Z,CPF_E7

LD A,(FCB+&09+&0B) SETCODE
LD (FCB2+&09+&0B),A

CPF_E7:
LD HL,FCB+&1F   DATUM ETC.
LD DE,FCB2+&1F
LD BC,&0004
LDIR

LD DE,FCB2
CALL CLOSEF
RET C

LD DE,FCB
CALL CLOSEF
RET C

CPF_E1:
JP LD_FMSK

OPENFF: OPEN FILE FROM
CALL SETFCB

LD C,OP_FL
LD DE,FCB
LD A,&01
LD (DE),A
CALL FILE
RET NC

OPEN01:
JP FEHLER

CLOSEALL: ALLE FILES
'         SCHLIESSEN
LD DE,FCB2
CALL CLOSEF
RET C

CLOSE1:
LD DE,FCB

CLOSEF: CLOSE FILE (FCB)
XOR A
LD (DE),A
PUSH BC
LD C,CL_FL   CLOSE
CALL FILE
POP BC
RET

FILE:  BS-FILE-COMMANDO
PUSH BC
PUSH DE
PUSH HL
CALL XFILE
POP HL
POP DE
POP BC
AND A
RET Z
LD A,(&F89B)
SCF
RET

AOUT: AKKU ALS 2HEXZIFFERN
'     AUSGEBEN
PUSH AF
PUSH BC
LD C,A
LD A,&F0
AND C
RRCA
RRCA
RRCA
RRCA
CALL BINASC
CALL PRTAKKU
LD A,&0F
AND C
CALL BINASC
CALL PRTAKKU
POP BC
POP AF
RET

BINASC: CONVERT A 2 ASC
CP &0A
JP M,BA_01
ADD A,&07

BA_01:
ADD A,&30
RET

CPTX01:
DEFB "COPY. Diskette klar?"
DEFB &00

RNTX01:
DEFB "B"

RNTX04:
DEFB "LOAD"
DEFB &22

RNTX02:
DEFS 20

RNTX03:
DEFB "Programm starten? "
DEFB &00

NMTX01:
DEFB "NAME "
DEFB &22
DEFB &00

NMTX02:
DEFB "  AS "
DEFB &22
DEFB &00

NM_TXT:
DEFB "Name as:"
DEFB &00
'DEFS FNLEN

SERTXT:
DEFB "Schnittstelle bereit?"
DEFB 0

